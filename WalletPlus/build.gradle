buildscript {
    repositories {
        mavenCentral()
        maven {url 'https://oss.sonatype.org/content/repositories/snapshots/'}
    }

    dependencies {
        classpath "com.android.tools.build:gradle:0.13.+"
        classpath "com.neenbedankt.gradle.plugins:android-apt:1.4"
        classpath "org.robolectric:robolectric-gradle-plugin:0.13.+"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'robolectric'

apt {
    arguments {
        androidManifestFile variant.processResources.manifestFile
        resourcePackageName 'info.korzeniowski.walletplus'
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.0.2'

    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    sourceSets {
        androidTest.setRoot( 'src/androidTest' )
    }
}

robolectric {
    include '**/*Test.class'
    exclude '**/espresso/**/*.class'
    maxHeapSize = "2048m"
}

repositories {
    mavenCentral()
    maven {url 'https://oss.sonatype.org/content/repositories/snapshots/'}
}

dependencies {
    // Android Support
    def androidSupportVersion = '21.0.+'
    compile "com.android.support:support-v4:$androidSupportVersion"
    compile "com.android.support:appcompat-v7:$androidSupportVersion"

    // OrmLite
    def ormLiteVersion = '4.48'
    compile "com.j256.ormlite:ormlite-android:$ormLiteVersion"
    compile "com.j256.ormlite:ormlite-core:$ormLiteVersion"

    // Butter Knife
    compile 'com.jakewharton:butterknife:5.1.2'

    // Dagger
    def daggerVersion = '1.2.+'
    apt "com.squareup.dagger:dagger-compiler:$daggerVersion"
    androidTestCompile "com.squareup.dagger:dagger-compiler:$daggerVersion"
    compile "com.squareup.dagger:dagger:$daggerVersion"

    // Otto
    compile "com.squareup:otto:1.3.5"

    // Guava
    compile "com.google.guava:guava:17.0"

    // PagerSlidingTabStrip
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'

    // FButton
    compile 'info.hoang8f:fbutton:1.0.5'

    // JUnit
    androidTestCompile "junit:junit:4.11"

    // Mockito
    androidTestCompile "org.mockito:mockito-core:1.9.5"

    // Robolectric
    androidTestCompile("org.robolectric:robolectric:2.4-SNAPSHOT") {
        exclude module: 'classworlds'
        exclude module: 'maven-artifact'
        exclude module: 'maven-artifact-manager'
        exclude module: 'maven-error-diagnostics'
        exclude module: 'maven-model'
        exclude module: 'maven-plugin-registry'
        exclude module: 'maven-profile'
        exclude module: 'maven-project'
        exclude module: 'maven-settings'
        exclude module: 'nekohtml'
        exclude module: 'plexus-container-default'
        exclude module: 'plexus-interpolation'
        exclude module: 'plexus-utils'
        exclude module: 'wagon-file'
        exclude module: 'wagon-http-lightweight'
        exclude module: 'wagon-http-shared'
        exclude module: 'wagon-provider-api'
    }

    //AssertJ Android
    androidTestCompile 'com.squareup.assertj:assertj-android:1.0.+'
}

apply plugin: "jacoco"

jacoco {
    toolVersion = "0.7.1.201405082137"
}
// Define coverage source.
// If you have rs/aidl etc... add them here.
def coverageSourceDirs = [
        'src/main/java',
        'src/gen'
]

task jacocoTestReport(type: JacocoReport, dependsOn: "testDebug") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled = false
        html.enabled = true
    }
    classDirectories = fileTree(
            dir: './build/intermediates/classes/debug',
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/*$InjectAdapter.class',
                       '**/*$ModuleAdapter.class',
                       '**/*$ViewInjector*.class'
            ])
    sourceDirectories = files(coverageSourceDirs)
    executionData = files('build/jacoco/testDebug.exec')

    def renamedFilesMap = [:]

    // Hacky fix for issue: https://code.google.com/p/android/issues/detail?id=69174.
    // Rename files with '$$' before generating report, and then rename back after
    doFirst {
        new File('WalletPlus/build/intermediates/classes/').eachFileRecurse { file ->
            if (file.name.contains('$$')) {
                def oldPath = file.path
                def newPath = oldPath.replace('$$', '$')
                file.renameTo(newPath)
                renamedFilesMap[newPath] = oldPath
            }
        }
    }
    doLast {
        renamedFilesMap.each() {
            newPath, oldPath ->
                new File(newPath).renameTo(oldPath)
        }
    }
}
