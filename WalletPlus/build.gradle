buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }

    dependencies {
        classpath "com.neenbedankt.gradle.plugins:android-apt:1.4"
        classpath 'com.github.jcandksolutions.gradle:android-unit-test:2.1.1'
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'

    defaultConfig {
        versionCode 1
        versionName "1.0.0-SNAPSHOT"
        applicationId 'info.korzeniowski.walletplus'

        minSdkVersion 17
        targetSdkVersion 21
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    signingConfigs {
        config {
            keyAlias signingKeyAlias
            storePassword signingStorePassword
            keyPassword signingKeyPassword
            storeFile file(signingStoreFile)
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.config
        }
        release {
            signingConfig signingConfigs.config
        }
    }
}

apply plugin: 'com.neenbedankt.android-apt'

apt {
    arguments {
        androidManifestFile variant.outputs[0].processResources.manifestFile
        resourcePackageName android.defaultConfig.applicationId
    }
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }

    // For SwipeListView
    maven { url 'http://clinker.47deg.com/nexus/content/groups/public' }
}

dependencies {
    // Android Support
    def androidSupportVersion = '21.0.+'
    compile "com.android.support:support-v4:$androidSupportVersion"
    compile "com.android.support:appcompat-v7:$androidSupportVersion"
    compile "com.android.support:cardview-v7:$androidSupportVersion"
    compile "com.android.support:recyclerview-v7:$androidSupportVersion"

    // Google Drive API
    compile 'com.google.android.gms:play-services-identity:6.5.87'
    compile 'com.google.android.gms:play-services-drive:6.5.87'

    // Retrofit
    compile 'com.squareup.retrofit:retrofit:1.9.0'

    //OkHttp
    compile 'com.squareup.okhttp:okhttp:2.2.0'

    // OrmLite
    def ormLiteVersion = '4.48'
    compile "com.j256.ormlite:ormlite-android:$ormLiteVersion"
    compile "com.j256.ormlite:ormlite-core:$ormLiteVersion"

    // Butter Knife
    compile 'com.jakewharton:butterknife:6.1.0'

    // Dagger
    def daggerVersion = '2.0-SNAPSHOT'
    apt "com.google.dagger:dagger-compiler:$daggerVersion"
    testCompile "com.google.dagger:dagger-compiler:$daggerVersion"
    compile "com.google.dagger:dagger:$daggerVersion"
    testCompile "com.google.dagger:dagger-compiler:$daggerVersion"
    provided 'org.glassfish:javax.annotation:10.0-b28'

    // Guava
    compile 'com.google.guava:guava:18.0'

    // Joda time android
    compile 'net.danlew:android.joda:2.5.0'

    // PagerSlidingTabStrip
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'

    // FButton
    compile 'info.hoang8f:fbutton:1.0.5'

    // SwipeListView
    compile('com.fortysevendeg.swipelistview:swipelistview:1.0-SNAPSHOT@aar') {
        transitive = true
    }

    // HelloCharts
    compile 'com.github.lecho:hellocharts-library:1.5.3@aar'

    // ColorPickerView
    compile project(':colorPickerView-library')

    // JUnit
    testCompile 'junit:junit:4.11'

    // Mockito
    testCompile 'org.mockito:mockito-core:1.9.5'

    // Robolectric
    testCompile 'org.robolectric:robolectric:2.4'

    // Fest Android
    testCompile 'com.squareup:fest-android:1.0.8'

    // Fluent Exception
    testCompile 'pl.wkr:fluent-exception-rule:1.0.1'
}

apply plugin: "jacoco"

jacoco {
    toolVersion = "0.7.1.201405082137"
}

def coverageSourceDirs = [
        'src/main/java',
        'src/gen'
]

task jacocoTestReport(type: JacocoReport, dependsOn: "testDebug") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled = false
        html.enabled = true
    }
    classDirectories = fileTree(
            dir: './build/intermediates/classes/debug',
            includes: [
                    '**/info/korzeniowski/walletplus/ui/**',
                    '**/info/korzeniowski/walletplus/service/**'],
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/*$InjectAdapter.class',
                       '**/*$ModuleAdapter.class',
                       '**/*$ViewInjector*.class'
            ])
    sourceDirectories = files(coverageSourceDirs)
    executionData = files('build/jacoco/testDebug.exec')
}

apply plugin: 'idea'

idea {
    module {
        downloadSources = true
    }
}
